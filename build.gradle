
buildscript {

    repositories {
        maven {
            url "http://dl.bintray.com/gesellix/gradle-plugins"
        }
        maven {
            url "https://jitpack.io"
        }
        mavenCentral()
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "com.bmuschko:gradle-docker-plugin:2.4.1"
        classpath "de.gesellix:gradle-debian-plugin:16"
    }

}

plugins {
    id 'idea'
    id 'sonar-runner'
    id 'net.researchgate.release' version '2.3.5'
}


repositories{
    mavenCentral()
}

ext {
    mesosVer = "0.25.0"
    imagePrefix = 'containersol'
}

idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.jacoco.reportPath", "${buildDir}/jacoco/test.exec"
    }
}

task setVersionInBashScript << {
    ant.replace(file: "bin/minimesos", token: "MINIMESOS_TAG=\"latest\"", value: "MINIMESOS_TAG=\"${project.version}\"")
    exec {
        workingDir "${project.rootDir}"
        commandLine "git", "commit", "-a", "-m", "[Release ${project.version}] pre-tag commit"
    }
    exec {
        workingDir "${project.rootDir}"
        commandLine "git", "push"
    }
}

task resetVersionInBashScript << {
    // have to use regular expression because the project version has changed and the previous is not kept
    ant.replaceregexp(file: "bin/minimesos", match: "MINIMESOS_TAG=\".*?\"", replace: "MINIMESOS_TAG=\"latest\"")
}

task releaseMinimesosContainerImage << {

    def command = "docker tag \$(docker images containersol/minimesos | grep 'latest' | awk '{print \$3}') containersol/minimesos:${project.version}"
    // executing via bash, so we can make use of its interpreter of the parameters. Otherwise pipes do not work
    exec {
        commandLine "bash", "-c", command
    }

    println "Pushing containersol/minimesos:${project.version} docker image to Docker Hub..."
    exec {
        commandLine "docker", "push", "containersol/minimesos:${project.version}"
    }

    println "Pushing containersol/minimesos:latest docker image to Docker Hub..."
    exec {
        commandLine "docker", "push", "containersol/minimesos:latest"
    }
}

preTagCommit.dependsOn setVersionInBashScript
// project.version changes after execution of updateVersion. Time to tag and to push container
updateVersion.dependsOn releaseMinimesosContainerImage
// project.version changes after execution of updateVersion
commitNewVersion.dependsOn resetVersionInBashScript

// working around https://github.com/researchgate/gradle-release/issues/144
release {
    buildTasks = ['releaseBuild']
}

task releaseBuild {
    dependsOn(
        'minimesos:clean',
        'minimesos:build'
    )
}

